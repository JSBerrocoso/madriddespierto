apply plugin: 'com.android.application'

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.3"

  defaultConfig {
    applicationId "com.madriddespierto.app"
    minSdkVersion 17
    targetSdkVersion 23
    versionCode 1
    versionName "1.0"
  }

  signingConfigs {
    release
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }
  }
}

def Properties props = new Properties()
def propFile = new File('signing.properties')
if (propFile.canRead()){
  props.load(new FileInputStream(propFile))

  if (props!=null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
      props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
    android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
    android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
    android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
    android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
  } else {
    println 'signing.properties found but some entries are missing'
    android.buildTypes.release.signingConfig = null
  }
}else {
  println 'signing.properties not found'
  android.buildTypes.release.signingConfig = null
}


dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  testCompile 'junit:junit:4.12'


  compile 'com.android.support:appcompat-v7:23.3.0'
  compile 'com.android.support:design:23.3.0'
  compile 'com.github.delight-im:Android-AdvancedWebView:v2.1.0'
  compile 'com.android.support:support-v4:23.3.0'
  compile 'com.jakewharton:butterknife:7.0.1'

  compile 'com.github.medyo:android-about-page:1.0.6'


}
